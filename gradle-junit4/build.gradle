plugins {
    id 'java'
}

configurations {
    aspectConfig
}

sourceCompatibility = 1.8

group 'org.example'
version '1.0-SNAPSHOT'

compileJava.options.encoding = 'utf-8'
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'utf-8'
}

repositories {
    mavenLocal()
    mavenCentral()
}

def adapterVersion = "[0,)"

dependencies {
    testImplementation "ru.testit:testit-java-commons:$adapterVersion"
    testImplementation "ru.testit:testit-adapter-junit4:$adapterVersion"
    testImplementation 'org.aspectj:aspectjrt:1.9.7'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.junit.platform:junit-platform-runner:1.6.3'
    aspectConfig "org.aspectj:aspectjweaver:1.9.7"
}

test {
    useJUnit()
    doFirst {
        def weaver = configurations.aspectConfig.find { it.name.contains("aspectjweaver") }
        jvmArgs += "-javaagent:$weaver"
    }
    testLogging {
        events = [ "failed", "skipped", "passed" ]
        showCauses = false
        showStackTraces = false
        showStandardStreams = true
    }

    environment 'TMS_URL', System.getProperty('tmsUrl')
    environment 'TMS_PRIVATE_TOKEN', System.getProperty('tmsPrivateToken')
    environment 'TMS_PROJECT_ID', System.getProperty('tmsProjectId')
    environment 'TMS_CONFIGURATION_ID', System.getProperty('tmsConfigurationId')
    environment 'TMS_TEST_RUN_ID', System.getProperty('tmsTestRunId')
    environment 'TMS_TEST_RUN_NAME', System.getProperty('tmsTestRunName')
    environment 'TMS_ADAPTER_MODE', System.getProperty('tmsAdapterMode')
    environment 'TMS_CERT_VALIDATION', System.getProperty('tmsCertValidation')
    environment 'TMS_TEST_IT', System.getProperty('testIt')
    environment 'TMS_AUTOMATIC_CREATION_TEST_CASES', System.getProperty('tmsAutomaticCreationTestCases')
}