import java.nio.file.Paths

plugins {
    id 'java'
}

configurations {
    aspectConfig
}

sourceCompatibility = 1.8

group 'org.example'
version '1.0-SNAPSHOT'

compileJava.options.encoding = 'utf-8'
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'utf-8'
    // Allows the adapter to accept real parameter names
    options.compilerArgs.add("-parameters")
}

repositories {
    mavenCentral()
    mavenLocal()
}

def adapterVersion = "1.6.0"

dependencies {
    def javaCommonsJar = System.getProperty("JAVA_COMMONS_JAR", "")
    def adapterJar = System.getProperty("ADAPTER_JAR", "")

    if (javaCommonsJar == "") {
        testImplementation "ru.testit:testit-java-commons:$adapterVersion"
    }
    else
    {
        testImplementation files(javaCommonsJar)
    }

    if (adapterJar == "") {
        testImplementation "ru.testit:testit-adapter-testng:$adapterVersion"
    }
    else
    {
        testImplementation files(adapterJar)
    }
    
    testImplementation 'org.aspectj:aspectjrt:1.9.7'
    testImplementation 'org.testng:testng:7.5'
    aspectConfig "org.aspectj:aspectjweaver:1.9.7"
}

test {
    useTestNG()
    doFirst {
        def weaver = configurations.aspectConfig.find { it.name.contains("aspectjweaver") }
        jvmArgs += "-javaagent:$weaver"
    }
//    systemProperty 'tmsTestRunName', System.clearProperty('tmsTestRunName')
}