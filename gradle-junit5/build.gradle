plugins {
    id "java"
}

configurations {
    aspectConfig
}

group "org.example"
version "1.0-SNAPSHOT"

repositories {
    mavenLocal()
    mavenCentral()
}

tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
    options.encoding = "utf-8"
}

def adapterVersion = "[0,)"

dependencies {
    testImplementation "ru.testit:testit-java-commons:$adapterVersion"
    testImplementation "ru.testit:testit-adapter-junit5:$adapterVersion"
    testImplementation "org.aspectj:aspectjrt:1.9.7"
    testImplementation "org.junit.platform:junit-platform-launcher:1.9.0"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.2"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.8.2"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.8.2"
    aspectConfig "org.aspectj:aspectjweaver:1.9.7"
}

compileTestJava {
    options.getCompilerArgs().add("-parameters")
}

test {
    useJUnitPlatform()
    testLogging {
        events = [ "failed", "skipped", "passed" ]
        showCauses = false
        showStackTraces = false
        showStandardStreams = true
    }
    doFirst {
        def weaver = configurations.aspectConfig.find { it.name.contains("aspectjweaver") }
        jvmArgs += "-javaagent:$weaver"
    }

    environment "TMS_URL", System.getProperty("tmsUrl")
    environment "TMS_PRIVATE_TOKEN", System.getProperty("tmsPrivateToken")
    environment "TMS_PROJECT_ID", System.getProperty("tmsProjectId")
    environment "TMS_CONFIGURATION_ID", System.getProperty("tmsConfigurationId")
    environment "TMS_TEST_RUN_ID", System.getProperty("tmsTestRunId")
    environment "TMS_TEST_RUN_NAME", System.getProperty("tmsTestRunName")
    environment "TMS_ADAPTER_MODE", System.getProperty("tmsAdapterMode")
    environment "TMS_CERT_VALIDATION", System.getProperty("tmsCertValidation")
    environment "TMS_TEST_IT", System.getProperty("testIt")
    environment "TMS_AUTOMATIC_CREATION_TEST_CASES", System.getProperty("tmsAutomaticCreationTestCases")
}