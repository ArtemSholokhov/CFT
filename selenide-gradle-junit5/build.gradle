plugins {
    id 'java'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

configurations {
    aspectConfig
}

repositories {
    mavenLocal()
    mavenCentral()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'utf-8'
}

def adapterVersion = "[0,)"

dependencies {
    testImplementation "ru.testit:testit-java-commons:$adapterVersion"
    testImplementation "ru.testit:testit-adapter-junit5:$adapterVersion"
    testImplementation "ru.testit:testit-adapter-selenide:$adapterVersion"
    testRuntimeOnly 'org.slf4j:slf4j-simple:2.0.6'
    testImplementation 'com.codeborne:selenide:6.19.1'
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.2"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.8.2"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.8.2"
    testImplementation 'org.aspectj:aspectjrt:1.9.7'
    aspectConfig "org.aspectj:aspectjweaver:1.9.7"
}

test {
    useJUnitPlatform()
    doFirst {
        def weaver = configurations.aspectConfig.find { it.name.contains("aspectjweaver") }
        jvmArgs += "-javaagent:$weaver"
    }
    testLogging {
        events = [ "failed", "skipped", "passed" ]
        showCauses = false
        showStackTraces = false
        showStandardStreams = true
    }
    environment 'TMS_URL', System.getProperty('tmsUrl')
    environment 'TMS_PRIVATE_TOKEN', System.getProperty('tmsPrivateToken')
    environment 'TMS_PROJECT_ID', System.getProperty('tmsProjectId')
    environment 'TMS_CONFIGURATION_ID', System.getProperty('tmsConfigurationId')
    environment 'TMS_TEST_RUN_ID', System.getProperty('tmsTestRunId')
    environment 'TMS_TEST_RUN_NAME', System.getProperty('tmsTestRunName')
    environment 'TMS_ADAPTER_MODE', System.getProperty('tmsAdapterMode')
    environment 'TMS_CERT_VALIDATION', System.getProperty('tmsCertValidation')
    environment 'TMS_TEST_IT', System.getProperty('testIt')
    environment 'TMS_AUTOMATIC_CREATION_TEST_CASES', System.getProperty('tmsAutomaticCreationTestCases')

}